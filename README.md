# Loggly

This library wraps the [Loggly](http://www.loggly.com) cloud-based logging service.

The Loggly library allows you to easily interface with the Loggly API, prepopulates your log messages with a variety of useful metadata, and bulk-sends your log messages to cut down on your agents' outbound HTTP traffic.

## constructor(*customerToken, [options]*)
To create a new Loggly object you will need your customer token. You may also supply an optional table with any of the following keys to further configure the Loggly object:

| key     | default     | notes                                             |
| ------- | ----------- | ------------------------------------------------- |
| id      | {agentId}   | A unique ID for the device/agent                  |
| tag     | electricimp | A comma separated list of tags for all log events |
| timeout | 15          | Time between sends (in seconds)                   |
| debug   | true        | Enables / disables server.log'ing log messages    |

```squirrel
// bulk send logs every minute
loggly <- Loggly("<-- CUSTOMER_TOKEN -->", { "timeout": 60 });
```

## Logging Methods

There are three methods (representing three log levels) that can be used to log messages - `log`, `warn`, and `err`. When any of the logging methods are called, the following set of metadata is added to the message:

```squirrel
{
    "id": string,       // the id set in the constructor
    "level": string,    // The log level (specified by what logging method was invoked)
    "timestamp": string // An ISO 8601 timestamp
}
```

All three of the logging methods can be invoked in the following ways:

### log/warn/err(*table*)

When a table is passed into one of the logging methods, each key/value pair will be added to the log message. If desired, the metadata can be overriden by including keys present in the metadata (i.e. `id`, `level`, `timestamp`).

In the following example, we send a message collected from the device, and override the timestamp metadata with a device-side timestamp:

```squirrel
// Log information sent from the device
device.on("data", function(data) {
    loggly.log({
        "timestamp": Loggly.ISODateTime(data.ts),
        "ssid": data.ssid,
        "rssi": data.rssi,
        "msg": data.msg
    });
});
```

### log/warn/err(*string, [...]*)

A string, or a [format string](https://electricimp.com/docs/squirrel/string/format/) with parameters can also be passed into the logging methods. When a string or format string is passed into the logging methods, it will be added to the log message with a key of `msg`.

```squirrel
// Log a string:
loggly.log("Hello World!");

// Log a format string:
loggly.log("%s %s", "Hello", "World!");
```

### log/warn/err(*object*)

Any other object passed into logging methods will be cast as a string with the `.tostring()` method and added to the log message with a key of `msg`.

```squirrel
loggly.log(123.456);
```

## len()

Returns the number of logs that are currently queued to send.

```squirrel
// Send logs hourly
loggly <- Loggly("<-- CUSTOMER_TOKEN -->", { "timeout": 3600 });

// Periodically check how many logs we have, and send if > 100
function loop() {
    imp.wakeup(15, loop);
    if (loggly.len() > 100) loggly.send();
}

loop();
```

## send()

The send method immediatly sends any queued logs and resets the timeout.

*See len() for example usage.*

## onError(*callback*)

The onError handler allows you to attach an error handler (the callback) to the Loggly class. The error handler will be invoked if the Loggly class ever encounters an error while sending the logs to Loggly (note: the Loggly class will automatically retry sending the logs until successful).

The onError callback takes a single parameter - an HTTP Response table containing the `statuscode`, `body`, and `headers` (see [http.sendasync](https://electricimp.com/docs/api/httprequest/sendasync/) for more information).

```squirrel
loggly.onError(function(resp) {
    server.error(resp.statuscode + " - " + resp.body);
});
```

## Loggly.ISODateTime(*[timestamp]*)

The Loggly class has a static method that can generate an ISO 8601 datetime object. The ISODateTime method has an optional parameter *timestamp* - a Unix timestamp (generated with [time()](https://electricimp.com/docs/squirrel/system/time/)).

If an ISO 8601 datetime object is included with the key "timestamp" it will override the system timestamp generated by the Loggly class. This is particulairly useful when logging data from the device:

```squirrel
// Device code

// Reads data, sends it to the agent, then goes to sleep for 1 hr
function SenseSendAndSleep() {
    // Sense the data
    local data = senseData();

    // Add a timestamp
    data.ts <- time();

    // Send the data
    agent.send("data", data);

    // Go to sleep
    imp.onidle(function() { server.sleepfor(3600); });
}
```

```squirrel
// Agent Code

device.on("data", function(data) {
    // grab and delete the timestamp
    local ts = delete data.ts;

    // Add an ISO Datetime object with the key "timestamp"
    data.timestamp <- Loggly.ISODateTime(ts);

    // Logs the data
    loggly.log(data);
});
```

# License

The Loggly library is licensed under the [MIT License](./LICENSE).
